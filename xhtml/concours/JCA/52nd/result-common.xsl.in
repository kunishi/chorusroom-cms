<?xml version="1.0" encoding="iso-2022-jp"?>
<!-- $Id: result-common.xsl.in,v 1.11 1999/11/20 14:54:08 kunishi Exp $ -->
<xsl:stylesheet version="1.0"
		xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
		xmlns:xt="http://www.jclark.com/xt"
		xmlns="http://www.w3.org/TR/REC-html40"
		extension-element-prefix="xt">

  <xsl:template match="大会">
    <xsl:apply-templates select="開催日別結果"/>
  </xsl:template>

  <!--
   xsl:template match="開催日別結果" は、各 encoding 別 xsl ファイル
   に定義されています。
  -->    

  <xsl:template name="main">
    <xsl:element name="html">
      <xsl:element name="head">
	<xsl:element name="title">
	  <xsl:value-of select="/大会/大会名"/>
	</xsl:element>
	<xsl:call-template name="additional-header"/>
      </xsl:element>
      <xsl:element name="body">
	<xsl:call-template name="encodinglink"/>
	<xsl:element name="h1">
	  <xsl:value-of select="/大会/大会名"/>
	</xsl:element>
	<xsl:element name="hr"/>
	<xsl:element name="dl">
	  <xsl:call-template name="開催日リスト"/>
	  <xsl:apply-templates select="開催場所"/>
	  <xsl:call-template name="審査員リスト"/>
	  <xsl:call-template name="報告者リスト"/>
	</xsl:element>
	<xsl:apply-templates select="注記"/>
	<xsl:call-template name="採点結果リンク"/>
	<xsl:element name="hr"/>
	<xsl:apply-templates select="部門結果"/>
	<xsl:call-template name="footer"/>
      </xsl:element>
    </xsl:element>
  </xsl:template>

  <xsl:template name="additional-header">
    <xsl:element name="link">
      <xsl:attribute name="href">
	<xsl:text>../../style/jca-concour.css</xsl:text>
      </xsl:attribute>
      <xsl:attribute name="type">
	<xsl:text>text/css</xsl:text>
      </xsl:attribute>
      <xsl:attribute name="rel">
	<xsl:text>stylesheet</xsl:text>
      </xsl:attribute>
    </xsl:element>
    <xsl:element name="link">
      <xsl:attribute name="href">
	<xsl:text>mailto:kunishi@c.oka-pu.ac.jp</xsl:text>
      </xsl:attribute>
      <xsl:attribute name="rev">
	<xsl:text>made</xsl:text>
      </xsl:attribute>
    </xsl:element>
  </xsl:template>
  
  <xsl:template name="encodinglink">
    <xsl:element name="p">
      <xsl:if test="$suffix=$utfhtmlsuffix">
	<xsl:element name="a">
	  <xsl:attribute name="href">
	    <xsl:value-of select="concat(@出力, $htmlsuffix)" />
	  </xsl:attribute>
	  <xsl:text>EUC-JPページ</xsl:text>
	</xsl:element>
      </xsl:if>
      <xsl:if test="$suffix=$htmlsuffix">
	<xsl:element name="a">
	  <xsl:attribute name="href">
	    <xsl:value-of select="concat(@出力, $utfhtmlsuffix)"/>
	  </xsl:attribute>
	  <xsl:text>UTF-8ページ</xsl:text>
	</xsl:element>
      </xsl:if>
    </xsl:element>
  </xsl:template>

  <xsl:template name="footer">
    <xsl:element name="hr"/>
    <xsl:element name="address">
      <xsl:element name="a">
	<xsl:attribute name="href">
	  <xsl:text>mailto:kunishi@c.oka-pu.ac.jp</xsl:text>
	</xsl:attribute>
        <xsl:text>国島丈生 &lt;kunishi@c.oka-pu.ac.jp&gt;</xsl:text>
      </xsl:element>
    </xsl:element>
    <xsl:element name="p">
      <xsl:text>この文書は</xsl:text>
      <xsl:value-of select="/大会/CVSID"/>
      <xsl:text>から自動的に生成されました。</xsl:text>
    </xsl:element>
  </xsl:template>

  <xsl:template name="開催日リスト">
    <xsl:if test="child::開催日">
      <xsl:element name="dt">
	<xsl:text>開催日</xsl:text>
      </xsl:element>
      <xsl:element name="dd">
	<xsl:apply-templates select="開催日"/>
      </xsl:element>
    </xsl:if>
  </xsl:template>

  <xsl:template match="開催日">
    <xsl:value-of select="."/>
    <xsl:text>(</xsl:text>
    <xsl:value-of select="./@曜日"/>
    <xsl:text>)</xsl:text>
    <xsl:if test="following-sibling::開催日">
      <xsl:text>・</xsl:text>
    </xsl:if>
  </xsl:template>

  <xsl:template match="開催場所">
    <xsl:element name="dt">
      <xsl:text>開催場所</xsl:text>
    </xsl:element>
    <xsl:element name="dd">
      <xsl:value-of select="."/>
    </xsl:element>
  </xsl:template>

  <xsl:template name="審査員リスト">
    <xsl:if test="child::審査員">
      <xsl:element name="dt">
	<xsl:text>審査員</xsl:text>
      </xsl:element>
      <xsl:element name="dd">
	<xsl:apply-templates select="審査員"/>
      </xsl:element>
    </xsl:if>
  </xsl:template>
  <xsl:template match="審査員">
    <xsl:value-of select="."/>
    <xsl:if test="@肩書 or @備考">
      <xsl:text>(</xsl:text>
      <xsl:if test="@肩書">
	<xsl:value-of select="@肩書"/>
	<xsl:if test="@備考"><xsl:text>、</xsl:text></xsl:if>
      </xsl:if>
      <xsl:if test="@備考"><xsl:value-of select="@備考"/></xsl:if>
      <xsl:text>)</xsl:text>
    </xsl:if>
    <xsl:if test="following-sibling::審査員">
      <xsl:text>、</xsl:text>
    </xsl:if>
  </xsl:template>

  <xsl:template name="報告者リスト">
    <xsl:element name="dt">
      <xsl:text>報告者</xsl:text>
    </xsl:element>
    <xsl:apply-templates select="報告者"/>
  </xsl:template>
  <xsl:template match="報告者">
    <xsl:element name="dd">
      <xsl:if test="@匿名希望[.='yes']">
	<xsl:text>匿名希望</xsl:text>
      </xsl:if>
      <xsl:if test="not(@匿名希望[.='yes'])">
	<xsl:apply-templates select="報告者氏名"/>
	<xsl:apply-templates select="email"/>
      </xsl:if>
    </xsl:element>
  </xsl:template>

  <xsl:template match="報告者氏名">
    <xsl:if test="@ペンネーム">
      <xsl:value-of select="@ペンネーム"/>
    </xsl:if>
    <xsl:if test="not(@ペンネーム)">
      <xsl:value-of select="."/>
    </xsl:if>
  </xsl:template>

  <xsl:template match="email">
    <xsl:if test="not(@email匿名[.='yes'])">
      <xsl:text> (</xsl:text>
      <xsl:value-of select="."/>
      <xsl:text>)</xsl:text>
    </xsl:if>
  </xsl:template>

  <xsl:template name="採点結果リンク">
    <xsl:if test=".//採点結果">
      <xsl:element name="ul">
	<xsl:element name="li">
	  <xsl:element name="p">
	    <xsl:text>採点表を</xsl:text>
	    <xsl:element name="a">
	      <xsl:attribute name="href">
		<xsl:value-of select="concat(@出力, '-saiten', $suffix)"/>
	      </xsl:attribute>
	      <xsl:text>別ページ</xsl:text>
	    </xsl:element>
	    <xsl:text>にまとめてあります。</xsl:text>
	  </xsl:element>
	</xsl:element>
      </xsl:element>
    </xsl:if>
  </xsl:template>

  <xsl:template match="注記">
    <xsl:element name="ul">
      <xsl:apply-templates/>
    </xsl:element>
  </xsl:template>
  <xsl:template match="注記事項">
    <xsl:element name="li">
      <xsl:element name="p">
	<xsl:value-of select="."/>
      </xsl:element>
    </xsl:element>
  </xsl:template>

  <xsl:template match="部門結果">
    <xsl:apply-templates select="部門名"/>
    <xsl:if test="団体[賞[.='シード']]">
      <xsl:element name="h3">
	<xsl:text>シード団体</xsl:text>
      </xsl:element>
      <xsl:element name="ul">
	<xsl:apply-templates select="団体[賞[.='シード']]"/>
      </xsl:element>
    </xsl:if>
    <xsl:if test="団体[賞[.='金賞']]">
      <xsl:element name="h3">
	<xsl:text>金賞</xsl:text>
      </xsl:element>
      <xsl:element name="ul">
	<xsl:apply-templates select="団体[賞[.='金賞']]"/>
      </xsl:element>
    </xsl:if>
    <xsl:if test="団体[賞[.='銀賞']]">
      <xsl:element name="h3">
	<xsl:text>銀賞</xsl:text>
      </xsl:element>
      <xsl:element name="ul">
	<xsl:apply-templates select="団体[賞[.='銀賞']]"/>
      </xsl:element>
    </xsl:if>
    <xsl:if test="団体[賞[.='銅賞']]">
      <xsl:element name="h3">
	<xsl:text>銅賞</xsl:text>
      </xsl:element>
      <xsl:element name="ul">
	<xsl:apply-templates select="団体[賞[.='銅賞']]"/>
      </xsl:element>
    </xsl:if>
    <xsl:if test="団体[not(賞)]">
      <xsl:element name="h3">
	<xsl:text>そのほか</xsl:text>
      </xsl:element>
      <xsl:element name="ul">
	<xsl:apply-templates select="団体[not(賞)]"/>
      </xsl:element>
    </xsl:if>
  </xsl:template>
  <xsl:template match="部門名">
    <xsl:element name="h2">
      <xsl:value-of select="."/>
      <xsl:if test="@開催日">
	<xsl:text> (</xsl:text>
	<xsl:value-of select="@開催日"/>
	<xsl:text>)</xsl:text>
      </xsl:if>
    </xsl:element>
  </xsl:template>

  <xsl:template match="団体">
    <xsl:element name="li">
      <xsl:if test="@大会代表[.='yes']">
	<xsl:text>◎</xsl:text>
      </xsl:if>
      <xsl:apply-templates select="団体名"/>
      <xsl:call-template name="団体データ"/>
      <xsl:call-template name="特別賞リスト"/>
      <xsl:apply-templates select="団体備考"/>
      <xsl:apply-templates select="曲目"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="団体名">
    <xsl:if test="@url">
      <xsl:element name="a">
	<xsl:attribute name="href">
	  <xsl:value-of select="@url"/>
	</xsl:attribute>
	<xsl:value-of select="."/>
      </xsl:element>
    </xsl:if>
    <xsl:if test="not(@url)">
      <xsl:value-of select="node()"/>
    </xsl:if>
  </xsl:template>

  <xsl:template name="団体データ">
    <xsl:if test="(所属県 and not(//大会名[@県大会])) or 形態 or 登録人数
	    or 指揮者 or ピアノ or 共演者">
      <xsl:text> (</xsl:text>
      <xsl:call-template name="団体データその1"/>
      <xsl:if test="((所属県 and not(//大会名[@県大会])) or 形態 or 登録人数)
	      and (指揮者 or ピアノ or 共演者)">
	<xsl:text>, </xsl:text>
      </xsl:if>
      <xsl:call-template name="演奏者データ"/>
      <xsl:text>)</xsl:text>
    </xsl:if>
  </xsl:template>

  <xsl:template name="団体データその1">
    <xsl:if test="not(//大会名[@県大会='yes'])">
      <xsl:apply-templates select="所属県"/>
    </xsl:if>
    <xsl:apply-templates select="形態"/>
    <xsl:apply-templates select="登録人数"/>
  </xsl:template>

  <xsl:template match="所属県">
    <xsl:value-of select="."/>
    <xsl:if test="following-sibling::形態 or following-sibling::登録人数">
      <xsl:text>・</xsl:text>
    </xsl:if>
  </xsl:template>

  <xsl:template match="形態">
    <xsl:value-of select="."/>
  </xsl:template>

  <xsl:template match="登録人数">
    <xsl:value-of select="."/>
    <xsl:text>名</xsl:text>
  </xsl:template>

  <xsl:template name="演奏者データ">
    <xsl:call-template name="指揮者リスト"/>
    <xsl:if test="ピアノ|共演者">
      <xsl:text>, </xsl:text>
    </xsl:if>
    <xsl:call-template name="ピアノリスト"/>
    <xsl:if test="共演者">
      <xsl:text>, </xsl:text>
    </xsl:if>
    <xsl:apply-templates select="共演者"/>
  </xsl:template>

  <xsl:template name="指揮者リスト">
    <xsl:for-each select="指揮者">
      <xsl:if test="position()=1">
	<xsl:text>指揮: </xsl:text>
      </xsl:if>
      <xsl:value-of select="."/>
      <xsl:if test="not(position()=last())">
	<xsl:text>・</xsl:text>
      </xsl:if>
    </xsl:for-each>
  </xsl:template>

  <xsl:template name="ピアノリスト">
    <xsl:for-each select="ピアノ">
      <xsl:if test="position()=1">
	<xsl:text>ピアノ: </xsl:text>
      </xsl:if>
      <xsl:value-of select="."/>
      <xsl:if test="not(position()=last())">
	<xsl:text>・</xsl:text>
      </xsl:if>
    </xsl:for-each>
  </xsl:template>

  <xsl:template match="共演者">
    <xsl:value-of select="@演奏楽器"/>
    <xsl:text>: </xsl:text>
    <xsl:value-of select="."/>
    <xsl:if test="following-sibling::共演者">
      <xsl:text>, </xsl:text>
    </xsl:if>
  </xsl:template>

  <xsl:template match="賞">
  </xsl:template>

  <xsl:template name="特別賞リスト">
    <xsl:if test="特別賞">
      <xsl:text> [</xsl:text>
      <xsl:for-each select="特別賞">
	<xsl:apply-templates/>
	<xsl:if test="not(position()=last())">
	  <xsl:text>, </xsl:text>
	</xsl:if>
      </xsl:for-each>
      <xsl:text>]</xsl:text>
    </xsl:if>
  </xsl:template>
  <xsl:template match="特別賞">
    <xsl:value-of select="."/>
  </xsl:template>

  <xsl:template match="団体備考">
    <xsl:text> (</xsl:text>
    <xsl:value-of select="."/>
    <xsl:text>)</xsl:text>
  </xsl:template>

  <xsl:template match="曲目">
    <xsl:element name="ul">
      <xsl:apply-templates select="課題曲"/>
      <xsl:apply-templates select="自由曲"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="課題曲">
    <xsl:element name="li">
      <xsl:value-of select="text()"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="自由曲">
    <xsl:element name="li">
      <xsl:apply-templates select="出典"/>
      <xsl:apply-templates select="作詩"/>
      <xsl:apply-templates select="訳詩"/>
      <xsl:apply-templates select="作曲"/>
      <xsl:apply-templates select="編曲"/>
      <xsl:if test="出典 or 作詩 or 訳詩 or 作曲 or 編曲">
  	<xsl:text>: </xsl:text>
      </xsl:if>
      <xsl:if test="曲名"><xsl:apply-templates select="曲名"/></xsl:if>
      <xsl:if test="組曲"><xsl:apply-templates select="組曲"/></xsl:if>
    </xsl:element>
  </xsl:template>

  <xsl:template match="出典">
    <xsl:value-of select="."/>
    <xsl:if test="following-sibling::作詩 or following-sibling::作曲
                  or following-sibling::編曲 or following-sibling::訳詩">
      <xsl:text>・</xsl:text>
    </xsl:if>
  </xsl:template>
  <xsl:template match="作詩">
    <xsl:value-of select="."/>
    <xsl:text>作詩</xsl:text>
    <xsl:if test="following-sibling::作曲">
      <xsl:text>・</xsl:text>
    </xsl:if>
  </xsl:template>
  <xsl:template match="作曲">
    <xsl:value-of select="."/>
    <xsl:text>作曲</xsl:text>
    <xsl:if test="following-sibling::編曲">
      <xsl:text>・</xsl:text>
    </xsl:if>
  </xsl:template>
  <xsl:template match="編曲">
    <xsl:value-of select="."/>
    <xsl:text>編曲</xsl:text>
  </xsl:template>
  <xsl:template match="訳詩">
    <xsl:value-of select="."/>
    <xsl:text>訳詩</xsl:text>
    <xsl:if test="following-sibling::作曲">
      <xsl:text>・</xsl:text>
    </xsl:if>
  </xsl:template>

  <xsl:template match="曲名">
    <xsl:value-of select="."/>
  </xsl:template>

  <xsl:template match="組曲">
    <xsl:value-of select="曲名"/>
    <xsl:if test="@抜粋 = 'yes'">
      <xsl:text>から </xsl:text>
    </xsl:if>
    <xsl:apply-templates select="構成曲"/>
  </xsl:template>

  <xsl:template match="構成曲">
    <xsl:if test="not(構成曲)">
      <xsl:text>「</xsl:text>
    </xsl:if>
    <xsl:if test="構成曲番号">
      <xsl:apply-templates select="構成曲番号" />
    </xsl:if>
    <xsl:apply-templates select="曲名"/>
    <xsl:if test="作詩 or 出典 or 訳詩">
      <xsl:text> (</xsl:text>
      <xsl:apply-templates select="出典"/>
      <xsl:apply-templates select="作詩"/>
      <xsl:apply-templates select="訳詩"/>
      <xsl:text>)</xsl:text>
    </xsl:if>
    <xsl:if test="構成曲">
      <xsl:apply-templates select="構成曲" />
    </xsl:if>
    <xsl:if test="not(構成曲)">
      <xsl:text>」</xsl:text>
    </xsl:if>
  </xsl:template>

  <xsl:template match="構成曲番号">
    <xsl:value-of select="." />
    <xsl:text> </xsl:text>
  </xsl:template>

</xsl:stylesheet>
